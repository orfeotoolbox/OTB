#
# This file sets up include directories, link directories, and
# compiler settings for a project to use OTB.  It should not be
# included directly, but rather through the OTB_USE_FILE setting
# obtained from OTBConfig.cmake.
#

IF(OTB_BUILD_SETTINGS_FILE)
  INCLUDE(${CMAKE_ROOT}/Modules/CMakeImportBuildSettings.cmake)
  CMAKE_IMPORT_BUILD_SETTINGS(${OTB_BUILD_SETTINGS_FILE})
ENDIF(OTB_BUILD_SETTINGS_FILE)

# Add compiler flags needed to use OTB.
SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${OTB_REQUIRED_C_FLAGS}")
SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OTB_REQUIRED_CXX_FLAGS}")
SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${OTB_REQUIRED_LINK_FLAGS}")
SET(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} ${OTB_REQUIRED_LINK_FLAGS}")
SET(CMAKE_MODULE_LINKER_FLAGS "${CMAKE_MODULE_LINKER_FLAGS} ${OTB_REQUIRED_LINK_FLAGS}")

# Add include directories needed to use OTB.
INCLUDE_DIRECTORIES(${OTB_INCLUDE_DIRS})

# Add link directories needed to use OTB.
LINK_DIRECTORIES(${OTB_LIBRARY_DIRS})

# Load settings for the system VXL with which OTB was built, if any.
IF(OTB_USE_SYSTEM_VXL)
  # If VXL has already been found, make sure it is the one used to
  # build OTB.
  IF(WIN32 OR APPLE)
    STRING(TOLOWER "${VXL_DIR}" OTB_CHECK_VXL_DIR)
    STRING(TOLOWER "${OTB_VXL_DIR}" OTB_CHECK_OTB_VXL_DIR)
  ELSE(WIN32 OR APPLE)
    SET(OTB_CHECK_VXL_DIR "${VXL_DIR}")
    SET(OTB_CHECK_OTB_VXL_DIR "${OTB_VXL_DIR}")
  ENDIF(WIN32 OR APPLE)
  STRING(COMPARE EQUAL "${OTB_CHECK_VXL_DIR}" "${OTB_CHECK_OTB_VXL_DIR}"
         OTB_VXL_DIR_MATCH)
  IF(VXL_FOUND)
    IF(NOT OTB_VXL_DIR_MATCH)
      MESSAGE(FATAL_ERROR
        "OTB was built with VXL from \"${OTB_VXL_DIR}\", "
        "but this project is using VXL from \"${VXL_DIR}\".  "
        "Please set VXL_DIR to match the one used to build OTB."
        )
    ENDIF(NOT OTB_VXL_DIR_MATCH)
  ELSE(VXL_FOUND)
    IF(VXL_DIR)
      IF(NOT OTB_VXL_DIR_MATCH)
        MESSAGE(
          "Warning: OTB was built with VXL from \"${OTB_VXL_DIR}\", "
          "but this project has set VXL_DIR to \"${VXL_DIR}\".  "
          "OTB is changing VXL_DIR to match the VXL with which it was built."
          )
      ENDIF(NOT OTB_VXL_DIR_MATCH)
    ENDIF(VXL_DIR)
    SET(VXL_DIR ${OTB_VXL_DIR})
    FIND_PACKAGE(VXL)
    IF(VXL_FOUND)
      INCLUDE(${VXL_CMAKE_DIR}/UseVXL.cmake)
    ELSE(VXL_FOUND)
      MESSAGE(FATAL_ERROR
        "UseOTB could not load VXL settings from \"${VXL_DIR}\" even through "
        "OTB was built using VXL from this location."
        )
    ENDIF(VXL_FOUND)
  ENDIF(VXL_FOUND)
ENDIF(OTB_USE_SYSTEM_VXL)

FIND_PROGRAM(OTB_TEST_DRIVER otbTestDriver PATHS
             ${OTB_BINARY_DIRS}
             ${OTB_BINARY_DIRS}/Release
             ${OTB_BINARY_DIRS}/MinSizeRel
             ${OTB_BINARY_DIRS}/RelWithDebInfo
             ${OTB_BINARY_DIRS}/Debug
             NO_SYSTEM_PATH)

FIND_PROGRAM(OTB_APPLICATION_LAUNCHER otbApplicationLauncherCommandLine PATHS
             ${OTB_BINARY_DIRS}
             ${OTB_BINARY_DIRS}/Release
             ${OTB_BINARY_DIRS}/MinSizeRel
             ${OTB_BINARY_DIRS}/RelWithDebInfo
             ${OTB_BINARY_DIRS}/Debug
             NO_SYSTEM_PATH)

IF(OTB_USE_EXTERNAL_ITK)
INCLUDE(${OTB_ITK_DIR}/ITKConfig.cmake)
INCLUDE(${ITK_USE_FILE})
ENDIF(OTB_USE_EXTERNAL_ITK)

IF(USE_FFTWF OR USE_FFTWD)
   FIND_PACKAGE(FFTW REQUIRED)
   INCLUDE_DIRECTORIES(${FFTW_INCLUDE_PATH})
ENDIF(USE_FFTWF OR USE_FFTWD)

IF(OTB_USE_EXTERNAL_EXPAT)
    FIND_PACKAGE(EXPAT)
    IF(EXPAT_FOUND)
        # Control expat compatibility
        INCLUDE_DIRECTORIES(${EXPAT_INCLUDE_DIR})
    ELSE(EXPAT_FOUND)
        MESSAGE(FATAL_ERROR
                  "Cannot use OTB project without EXPAT library. Please set EXPAT_DIR or rebuild OTB and set OTB_USE_EXTERNAL_EXPAT OFF to use INTERNAL EXPAT set on OTB/Utilities repository.")
    ENDIF(EXPAT_FOUND)
ELSE(OTB_USE_EXTERNAL_EXPAT)
    ADD_DEFINITIONS(-DOTB_USE_INTERNAL_EXPAT)
ENDIF(OTB_USE_EXTERNAL_EXPAT)


IF(OTB_USE_EXTERNAL_BOOST)
  FIND_PACKAGE(Boost)
  INCLUDE_DIRECTORIES(${Boost_INCLUDE_DIR})

  # needed for automatic linking on msvc platform
  LINK_DIRECTORIES( ${Boost_LIBRARY_DIRS} )
ENDIF(OTB_USE_EXTERNAL_BOOST)

IF(OTB_USE_EXTERNAL_GDAL)
  FIND_PACKAGE(GDAL)

  IF (NOT GDAL_FOUND)
    FIND_PATH(GDAL_INCLUDE_DIR gdal.h $ENV{GDAL_INCLUDE_DIR} /usr/include/gdal)
    FIND_LIBRARY(GDAL_LIBRARY NAMES gdal PATHS /usr/lib/gdal)
  ENDIF(NOT GDAL_FOUND)
  INCLUDE_DIRECTORIES(${GDAL_INCLUDE_DIR})
ENDIF(OTB_USE_EXTERNAL_GDAL)


if(OTB_USE_EXTERNAL_FLTK)
	FIND_PACKAGE(FLTK)
	IF(NOT FLTK_FOUND)
	   MESSAGE(FATAL_ERROR
             "OTB was built with an external FLTK library, but I could not find it. Please set FLTK_DIR to the path containing FLTKConfig.cmake.")
	ENDIF(NOT FLTK_FOUND)

	LINK_DIRECTORIES(${FLTK_LIBRARY_DIRS})
else(OTB_USE_EXTERNAL_FLTK)
  FIND_PROGRAM(FLTK_FLUID_EXECUTABLE fluid PATHS
               ${OTB_BINARY_DIRS}
               ${OTB_BINARY_DIRS}/Release
               ${OTB_BINARY_DIRS}/MinSizeRel
               ${OTB_BINARY_DIRS}/RelWithDebInfo
               ${OTB_BINARY_DIRS}/Debug
               NO_SYSTEM_PATH)
endif(OTB_USE_EXTERNAL_FLTK)

IF(OTB_USE_LIBLAS)
IF(OTB_USE_EXTERNAL_LIBLAS)
    FIND_PATH(LIBLAS_INCLUDE_DIR liblas.hpp)
    IF(LIBLAS_INCLUDE_DIR)
      INCLUDE_DIRECTORIES(${LIBLAS_INCLUDE_DIR})
    ENDIF(LIBLAS_INCLUDE_DIR)

    FIND_LIBRARY(LIBLAS_LIBRARY NAMES liblas_c liblas)

    IF (NOT LIBLAS_LIBRARY)
        MESSAGE(FATAL_ERROR
                "Cannot find Liblas library. Please set LIBLAS_LIBRARY")
    ENDIF (NOT LIBLAS_LIBRARY)
ENDIF(OTB_USE_EXTERNAL_LIBLAS)
ENDIF(OTB_USE_LIBLAS)

IF(OTB_USE_MAPNIK)
        FIND_PATH(MAPNIK_INCLUDE_DIR mapnik/map.hpp PATHS)
        MARK_AS_ADVANCED(MAPNIK_INCLUDE_DIR)

        FIND_LIBRARY(MAPNIK_LIBRARY NAMES mapnik2 mapnik)
        MARK_AS_ADVANCED(MAPNIK_LIBRARY)

        FIND_PATH(FREETYPE2_INCLUDE_DIR freetype/freetype.h PATHS /usr/include/freetype2)
        MARK_AS_ADVANCED(FREETYPE2_INCLUDE_DIR)

        # libicuuc (http://site.icu-project.org/) is a unicode library mapnik relies on.
        # mapnik includes drag icu includes
        # when linking with mapnik, need to link with libicuuc (at least since 1.42 release)
        FIND_PATH(ICUUC_INCLUDE_DIR unicode/unistr.h)
        MARK_AS_ADVANCED(ICUUC_INCLUDE_DIR)

        FIND_LIBRARY(ICUUC_LIBRARY icuuc)
        MARK_AS_ADVANCED(ICUUC_LIBRARY)

        # ltdl (libtool dl)
        FIND_PATH(LTDL_INCLUDE_DIR ltdl.h)
        MARK_AS_ADVANCED(LTDL_INCLUDE_DIR)

        FIND_LIBRARY(LTDL_LIBRARY ltdl)
        MARK_AS_ADVANCED(LTDL_LIBRARY)

        IF (NOT MAPNIK_INCLUDE_DIR)
                MESSAGE(FATAL_ERROR
                        "Cannot find MAPNIK include directory. Please set MAPNIK_INCLUDE_DIR or set OTB_USE_MAPNIK OFF.")
        ENDIF (NOT MAPNIK_INCLUDE_DIR)
        IF (NOT MAPNIK_LIBRARY)
                MESSAGE(FATAL_ERROR
                        "Cannot find MAPNIK library. Please set MAPNIK_LIBRARY or set OTB_USE_MAPNIK OFF.")
        ENDIF (NOT MAPNIK_LIBRARY)

        IF (NOT FREETYPE2_INCLUDE_DIR)
                MESSAGE(FATAL_ERROR
                        "Cannot find FREETYPE2 include directory. Please set FREETYPE2_INCLUDE_DIR or set OTB_USE_MAPNIK OFF.")
        ENDIF (NOT FREETYPE2_INCLUDE_DIR)

        IF (NOT ICUUC_INCLUDE_DIR)
                MESSAGE(FATAL_ERROR
                        "Cannot find ICUUC_INCLUDE_DIR include directory. Please set ICUUC_INCLUDE_DIR or set OTB_USE_MAPNIK OFF.")
        ENDIF (NOT ICUUC_INCLUDE_DIR)
        IF (NOT ICUUC_LIBRARY)
                MESSAGE(FATAL_ERROR
                        "Cannot find ICUUC library, needed by MAPNIK. Please set ICUUC_LIBRARY or set OTB_USE_MAPNIK OFF.")
        ENDIF (NOT ICUUC_LIBRARY)

        IF (NOT LTDL_INCLUDE_DIR)
                MESSAGE(FATAL_ERROR
                        "Cannot find LTDL_INCLUDE_DIR include directory. Please set LTDL_INCLUDE_DIR or set OTB_USE_MAPNIK OFF.")
        ENDIF (NOT LTDL_INCLUDE_DIR)
        IF (NOT LTDL_LIBRARY)
                MESSAGE(FATAL_ERROR
                        "Cannot find ICUUC library, needed by MAPNIK. Please set ICUUC_LIBRARY or set OTB_USE_MAPNIK OFF.")
        ENDIF (NOT LTDL_LIBRARY)

        IF (NOT OTB_MAPNIK_SUPPORTS_API20)
          ADD_DEFINITIONS(-DOTB_MAPNIK_COMPATIBILITY_API07)
        ENDIF (NOT OTB_MAPNIK_SUPPORTS_API20)

        # Add compiler option
        ADD_DEFINITIONS(-DOTB_USE_MAPNIK)

        INCLUDE_DIRECTORIES(${MAPNIK_INCLUDE_DIR} ${ICUUC_INCLUDE_DIR} ${LTDL_INCLUDE_DIR}  ${FREETYPE2_INCLUDE_DIR})

ENDIF(OTB_USE_MAPNIK)

IF(OTB_USE_PQXX)

        FIND_PATH(PQXX_INCLUDE_DIR pqxx/pqxx PATHS)
        MARK_AS_ADVANCED(PQXX_INCLUDE_DIR)
        IF (NOT PQXX_INCLUDE_DIR)
                MESSAGE(FATAL_ERROR
                        "Cannot find PQXX include directory. Please set PQXX_INCLUDE_DIR or SET OTB_USE_PQXX OFF.")
        ENDIF (NOT PQXX_INCLUDE_DIR)

        FIND_LIBRARY(PQXX_LIBRARY pqxx )
        MARK_AS_ADVANCED(PQXX_LIBRARY)
        IF (NOT PQXX_LIBRARY)
                MESSAGE(FATAL_ERROR
                        "Cannot find PQXX library. Please set PQXX_LIBRARY or SET OTB_USE_PQXX OFF.")
        ENDIF (NOT PQXX_LIBRARY)

        FIND_LIBRARY(PQ_LIBRARY pq )
        MARK_AS_ADVANCED(PQ_LIBRARY)
        IF (NOT PQ_LIBRARY)
                MESSAGE(FATAL_ERROR
                        "Cannot find PQ library. Please set PQ_LIBRARY or SET OTB_USE_PQXX OFF.")
        ENDIF (NOT PQ_LIBRARY)

        INCLUDE_DIRECTORIES(${PQXX_INCLUDE_DIR})

ENDIF(OTB_USE_PQXX)

INCLUDE(${OTB_LIBRARY_DIRS}/OTBParseArguments.cmake)
INCLUDE(${OTB_LIBRARY_DIRS}/OTBWrapperMacros.cmake)

